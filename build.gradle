buildscript {
    ext.kotlin_version = '1.2.50'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.hidetake:gradle-ssh-plugin:2.9.0'
    }
}

group 'net.panelpi'
version '1.0-beta-1'

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'org.hidetake.ssh'

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/groups/public" }
}

sourceSets {
    main {
        kotlin {
            srcDirs = ["src/main/kotlin"]
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'com.pi4j:pi4j-core:1.2-SNAPSHOT'
    compile 'com.pi4j:pi4j-gpio-extension:1.2-SNAPSHOT'
    compile 'org.json:json:20180130'
    compile 'no.tornado:tornadofx:1.7.16'
    compile 'org.fxmisc.easybind:easybind:1.0.3'

    compile 'de.jensd:fontawesomefx-fontawesome:4.7.0'
    compile 'de.jensd:fontawesomefx-materialdesignfont:1.7.22'

    compile 'org.apache.logging.log4j:log4j-core:2.11.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.0'
    compile 'io.github.microutils:kotlin-logging:1.5.4'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task fatJar(type: Jar) {
    group = 'build'
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': "net.panelpi.PanelPiApp"
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

remotes {
    pi {
        host = 'panelpi'
        user = 'pi'
        identity = file("${System.getProperty("user.home")}/.ssh/id_rsa")
    }
}

task deployToPi(dependsOn: fatJar) {
    group = 'raspberrypi'
    doLast {
        ssh.run {
            session(remotes.pi) {
                //remove "piFeeder/piFeeder-${project.version}.jar"
                put from: "${buildDir}/libs/${project.name}-all-${project.version}.jar", into: "panelPi/panelPi.jar"
            }
        }
    }
}
task runOnPi {
    group = 'raspberrypi'
    doLast {
        ssh.run {
            session(remotes.pi) {
                execute "sudo killall java || true"
                execute "sudo java " +
                        "-Dprism.order=es2,es1,sw,j2d " +
                        "-Dsun.java2d.opengl=true " +
                        "-Dprism.vsync=false " +
                        "-Dprism.forceGPU=true " +
                        "-Djava.ext.dirs=/home/pi/jdk1.8.0_92/jre/lib/ext/ " +
                        "-jar panelPi/panelPi.jar"
            }
        }
    }
}
